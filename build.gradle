plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id 'maven'
}

group = 'ch.njol'
version = '2.2-SNAPSHOT'

description = "Skript"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    maven { url "http://maven.njol.ch/repo/" }
    maven { url "http://maven.sk89q.com/repo/" }
    maven { url "http://nexus.hc.to/content/repositories/pub_releases" }
    maven { url "http://repo.maven.apache.org/maven2" }
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
}

dependencies {
    compile("org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT") {
        exclude module: 'bungeecord-chat'
    }
    compile(group: 'ch.njol', name: 'utils', version: '1.0-SNAPSHOT') {
        transitive = false
    }
    compile(group: 'ch.njol', name: 'yggdrasil', version: '0.1') {
        exclude module: 'jdt.annotation'
        exclude module: 'utils'
    }
    compile group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.annotation', version: '1.1.200'
    compile group: 'com.google.code.findbugs', name: 'findbugs', version: '2.0.3'
    compile(group: 'patpeter', name: 'sqlibrary', version: '4.0')
    compile(group: 'net.milkbowl.vault', name: 'Vault', version: '1.6.7') {
        transitive = false
    }
    compile(group: 'com.massivecraft', name: 'factions', version: '2.6')
    compile(group: 'com.massivecraft', name: 'mcore', version: '7.4')
    compile(group: 'com.sk89q', name: 'worldguard', version: '6.0.0-SNAPSHOT') {
        exclude module: 'bukkit'
    }
    compile(group: 'net.sacredlabyrinth.phaed', name: 'preciousstones', version: '9.7')
    compile(group: 'me.ryanhamshire', name: 'griefprevention', version: '8.2')
    testCompile group: 'junit', name: 'junit', version: '4.8.2'
    testCompile group: 'org.easymock', name: 'easymock', version: '3.0'
}

shadowJar {
    dependencies {
        include(dependency('ch.njol:yggdrasil:0.1'))
        include(dependency('ch.njol:utils:1.0-SNAPSHOT'))
        classifier = ''
    }
}

artifacts {
    archives shadowJar
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}
